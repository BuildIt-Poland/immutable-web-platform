# https://eksworkshop.com/scaling/deploy_hpa/
{ 
  config, 
  pkgs,
  lib, 
  kubenix, 
  k8s-resources ? pkgs.k8s-resources,
  project-config,
  ... 
}:
let
  namespace = project-config.kubernetes.namespace;
  eks-ns = "eks";
  kn-serving = namespace.knative-serving;
  istio-ns = namespace.istio;
  functions-ns = namespace.functions;

  update-eks-vpc-cni = 
    pkgs.writeScriptBin "apply-aws-credentails-secret" ''
      ${pkgs.lib.log.important "Patching AWS VPC CNI"}

      ${pkgs.kubectl}/bin/kubectl patch daemonset aws-node \
        -n kube-system \
        -p '{"spec": {"template": {"spec": {"containers": [{"image": "602401143452.dkr.ecr.us-west-2.amazonaws.com/amazon-k8s-cni:v1.5.1-rc1","name":"aws-node"}]}}}}'
    '';

  # https://knative.dev/docs/serving/tag-resolution/
  # https://github.com/knative/serving/issues/4435#issuecomment-504108797 
  # https://github.com/knative/serving/pull/4084
  knative-not-resolve-tags =
    pkgs.writeScriptBin "knative-not-resolve-tags" ''
      ${pkgs.lib.log.important "Patching Knative serving"}

      ${pkgs.kubectl}/bin/kubectl patch configmap config-deployment \
        -n ${kn-serving} \
        -p '{"data":{"registriesSkippingTagResolving":"${project-config.aws.account}.dkr.ecr.${project-config.aws.region}.amazonaws.com/${project-config.kubernetes.cluster.name}"}}'
    '';

  # aws route53 list-hosted-zones-by-name --output json --dns-name "local-future-is-comming.io" | jq -r '.HostedZones[0].Id
  create-cr = kind: resource: {
    inherit kind resource;

    group = "certmanager.k8s.io";
    version = "v1alpha1";
    description = "";
  };

  create-cert-mgr-cr = kind: {
    inherit kind;
    group = "certmanager.k8s.io";
    version = "v1alpha1";
    description = "";
  };

  iam-annotations = {
      "iam.amazonaws.com/allowed-roles" = "[\"${project-config.kubernetes.cluster.name}*\"]";
  };
  # TODO helm chart does not allow to override tls mode
  # https://github.com/istio/istio/blob/master/install/kubernetes/helm/istio/charts/gateways/templates/preconfigured.yaml#L22
  # other option would be to override istio-autogenerated-k8s-ingress (which is static as well ...)
  patch-ingressgateway = pkgs.writeScriptBin "patch-ingressgateway" ''
    ${pkgs.kubectl}/bin/kubectl -n ${istio-ns} \
      patch gateway istio-autogenerated-k8s-ingress --type=json \
      -p='[{"op": "replace", "path": "/spec/servers/1/tls", "value": {"credentialName": "ingress-cert", "mode": "SIMPLE", "privateKey": "sds", "serverCertificate": "sds"}}]'
  '';
in
{
  imports = with kubenix.modules; [ 
    k8s
    k8s-extension
    helm
    istio-service-mesh
    ./virtual-services.nix
  ];

  kubernetes.patches = [
    update-eks-vpc-cni
    knative-not-resolve-tags
    patch-ingressgateway
  ];

  kubernetes.annotations = {
    instance-on-demand = {"kubernetes.io/lifecycle"= "on-demand";};
  };

  kubernetes.api.namespaces."${eks-ns}"= {
    metadata.annotations = iam-annotations;
  };
 
   # https://github.com/terraform-aws-modules/terraform-aws-eks/blob/master/docs/autoscaling.md
  kubernetes.helm.instances.eks-cluster-autoscaler = {
    namespace = "${eks-ns}";
    chart = k8s-resources.cluster-autoscaler;
    values = {
      rbac.create = "true";
      cloudProvider = "aws";
      sslCertPath =  "/etc/ssl/certs/ca-bundle.crt"; # it is necessary in case of EKS
      awsRegion = project-config.aws.region;
      autoDiscovery = {
        clusterName = "${project-config.kubernetes.cluster.name}";
        enabled = true;
      };
      nodeSelector = config.kubernetes.annotations.instance-on-demand;
    };
  };

  # crd -> https://github.com/helm/charts/blob/master/stable/external-dns/templates/crd.yaml
  # example: https://github.com/kubernetes-incubator/external-dns/blob/master/docs/contributing/crd-source/dnsendpoint-example.yaml
  kubernetes.helm.instances.external-dns = {
    namespace = "${eks-ns}";
    chart = k8s-resources.external-dns;
    values = {
      provider = "aws"; 
      istioIngressGateways = [
        "istio-system/istio-ingressgateway"
      ];
      sources = ["service" "ingress" "istio-gateway"];
      rbac.create = true;
      policy = "upsert-only";
      logLevel = "debug";
      aws = {
        region = project-config.aws.region;
      };
      domainFilters = [project-config.project.domain];
    };
  };

  kubernetes.helm.instances.kube2iam = {
    namespace = "${eks-ns}";
    chart = k8s-resources.kube2iam;
    values = {
      rbac.create = true;
    };
  };

  # actually it make sense to have issuer and manager here
  kubernetes.api.cert-manager-certificates = {
    ingress-cert = 
    let
      mk-domain = project-config.project.make-sub-domain;
    in
    {
      metadata = {
        namespace = istio-ns;
        name = "ingress-cert";
      };
      spec = {
        secretName = "ingress-cert";
        issuerRef = {
          name = "cert-issuer";
          kind = "ClusterIssuer";
        };
        commonName = "${mk-domain "*"}";
        # should be field subdomains = ["*", "functions", ...]
        dnsNames = [ 
          (mk-domain "*") 
          (mk-domain "*.${functions-ns}") 
        ];
        acme.config = [
          { dns01.provider = "route53";
            domains = [ 
              (mk-domain "*")
              (mk-domain "*.${functions-ns}") 
            ];
          }
        ];
        };
      };
    };

  kubernetes.api.cert-manager-issuer = {
    ingress-cert =  {
      metadata = {
        name = "cert-issuer";
      };
      spec = {
        acme = {
          server = "https://acme-v02.api.letsencrypt.org/directory";
          email = project-config.project.author-email;
          privateKeySecretRef.name = "cert-prod";
          dns01.providers = [{ 
            name = "route53"; 
            route53 = {
              region = project-config.aws.region; 
            };
          }];
        };
      };
    };
  };

  kubernetes.network-mesh = {
    enable = true;

    crd = {
      certmanager.enabled = true;
    };

    namespace = {
      metadata.annotations = {
        "iam.amazonaws.com/allowed-roles" = "[\"${project-config.kubernetes.cluster.name}*\"]";
      };
    };

    helm = {
      gateways = {
        istio-ingressgateway = {
          sds.enabled = true;
          serviceAnnotations = {
            "certmanager.k8s.io/acme-challenge-type" = "dns01";
            "certmanager.k8s.io/acme-dns01-provider" = "aws";
            "certmanager.k8s.io/cluster-issuer" = "cert-issuer";
            "domainName" = "${project-config.project.make-sub-domain ""}";
            "external-dns.alpha.kubernetes.io/hostname" = "${project-config.project.make-sub-domain "*"}";
            "kubernetes.io/tls" = "ingress-cert";
            "kubernetes.io/tls-acme" = "true";
            "service.beta.kubernetes.io/aws-load-balancer-additional-resource-tags" = "Owner=${project-config.project.author-email}";
          };
        };
      };

      global = {
        mtls.enabled = true;

        sds = {
          enabled = true;
          udsPath = "unix:/var/run/sds/uds_path";
          useNormalJwt = true;
        };

        controlPlaneSecurityEnabled = false;
        k8sIngress.enableHttps = true;
      };

      nodeagent = {
        enabled =  true;
        image =  "node-agent-k8s";
        env = {
          CA_PROVIDER =  "Citadel";
          CA_ADDR =  "istio-citadel:8060";
          VALID_TOKEN = true;
        };
      };

      certmanager.enabled = true;
      certmanager.email = project-config.project.author-email;
    };
  };

  # FIXME helm stable/k8s-spot-termination-handler

  kubernetes.customResources = [
    (create-cr "Certificate" "cert-manager-certificates")
    (create-cr "ClusterIssuer" "cert-manager-issuer")
  ];
}
